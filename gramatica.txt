<start> -> <inc> <dec1>

<inc> -> `importar' STRING `;' <inc> | LAMBDA

<dec1> -> <dec> <dec0>
<dec0> -> <dec1> | LAMBDA
<dec> -> <const-dec> | <var-dec> | <func-dec> | <struct-dec> | <enum-dec>

<const-dec> -> `const' <type> ID <expr> `;'

<var-dec1> -> <var-dec> <var-dec0>
<var-dec0> -> <var-dec1> | LAMBDA
<var-dec> -> <type> <id-dec1> `;'

<id-dec1> -> ID <id-init> <id-dec0>
<id-dec0> -> `,' <id-dec1> | LAMBDA
<id-init> -> `=' <expr> | LAMBDA

<struct-dec> -> `estrutura' ID `{' <var-dec1> `}'

<enum-dec> -> `enum' ID `{' <ids1> `}'
<ids1> -> ID <ids0>
<ids0> -> `,' <ids1> | LAMBDA

<func-dec> -> <func-sign> <func-end>
<func-end> -> `;' | <block>
<func-sign> -> <type> ID `(' <params> `)'

<params> -> <param1> | LAMBDA
<param1> -> <type> ID <param0>
<param0> -> `,' <param1> | LAMBDA

<type> -> <basic-type> <array-type> | `(' <type> `)'
<basic-type> -> ID | `int' | `caractere' | `string' | `real' | `reald' | `vazio' | `*' <type>
<array-type> -> `[' <expr> `]' <array-type> | LAMBDA

<block> -> `{' <stmts> `}'
<stmts> -> <stmt> <stmts> | LAMBDA

<stmt> -> <dec> | <if> | <switch-case> | <for> | <while> | <do-while> | <block> | <expr> `;' | `retornar' `;' | `retornar' <expr> `;' | `parar' `;' | `continuar' `;' | `irpara' ID `;' | ID `:'

<if> -> `se' `(' <expr> `)' <stmt> <else>
<else> -> `senao' <stmt> | LAMBDA

<while> -> `enquanto' `(' <expr> `)' <stmt>
<do-while> -> `faz' <stmt> `enquanto' `(' <expr> `)' `;'

<for> -> `para' ID `de' `(' <expr> `)' <for-expr>
<for-expr> -> <for-asc> | <for-desc>
<for-asc> -> `asc' `(' <expr> `)' <stmt>
<for-desc> -> `desc' `(' <expr> `)' <stmt>

<switch-case> -> `escolha' `(' <expr> `)' `{' <case1> `}' 
<case1> -> <case> <case0>
<case0> -> <case1> | LAMBDA
<case> -> `caso' <expr> `:' <stmts> | `cc' `:' <stmts>

<expr> -> <logor-chain>
<expr-leaf> -> <func-call> | <expr-lit> | `(' <expr> `)'

<logor-chain> -> <logand-chain> <logor-tail>
<logor-tail> -> <logor-op> <logand-chain> <logor-tail> | LAMBDA

<logand-chain> -> <bitor-chain> <logand-tail>
<logand-tail> -> <logand-op> <bitor-chain> <logand-chain> | LAMBDA

<bitor-chain> -> <bitand-chain> <bitor-tail>
<bitor-tail> -> <bitor-op> <bitand-chain> <bitor-chain> | LAMBDA

<bitand-chain> -> <eq-chain> <bitand-tail>
<bitand-tail> -> <bitand-op> <eq-chain> <bitand-chain> | LAMBDA

<eq-chain> -> <rel-chain> <eq-tail>
<eq-tail> -> <eq-op> <rel-chain> <eq-chain> | LAMBDA

<rel-chain> -> <shift-chain> <rel-tail>
<rel-tail> -> <rel-op> <shift-chain> | LAMBDA

<shift-chain> -> <add-chain> <shift-tail>
<shift-tail> -> <shift-op> <add-chain> <shift-chain> | LAMBDA

<add-chain> -> <mul-chain> <add-tail>
<add-tail> -> <add-op> <mul-chain> <add-chain> | LAMBDA

<mul-chain> -> <un-chain> <mul-tail>
<mul-tail> -> <mul-op> <un-chain> <mul-chain> | LAMBDA

<func-call> -> <expr-var> <call> 
<call> -> `(' <args> `)' | LAMBDA
<args1> -> <expr> <args0>
<args0> -> `,' <args> | LAMBDA
<args> -> <args1> | LAMBDA

<expr-lit> -> INTEIRO | REAL | STRING | CARACTERE | <array-lit>
<array-lit> -> `{' <args1> `}'

<expr-var> -> ID <var-mods>
<var-mods> -> `.' ID <var-mods> | `[' <expr> `]' <var-mods> | LAMBDA

<mul-op> -> `*' | `/' | `%'
<un-op> -> `!' | `-' | `+' | `*'
<add-op> -> `+' | `-'
<shift-op> -> `<<' | `>>'
<rel-op> -> `>' | `<' | `>=' | `<='
<eq-op> -> `==' | `!='
<bitand> -> `&'
<bitor> -> `|'
<logand> -> `&&'
<logor> -> `||'
<ternary> -> `?'`:'

<un-chain> -> <expr-leaf> <un-tail>
<un-tail> -> <un-op> <un-chain> | LAMBDA